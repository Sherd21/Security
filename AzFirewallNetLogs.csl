let materializedData =
    materialize(
    AzureDiagnostics
    | where Category == "AzureFirewallNetworkRule"
    | project msg_s, Resource, TimeGenerated);
union
    (
    // Azure Firewall Networking - Standard Log
    materializedData
    | where msg_s !has "Type=" and msg_s !has "DNAT'ed" and msg_s !has "Rule Collection:" and msg_s !has "Policy:" and msg_s !has "IDS:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort ". Action: " Action ". "
    ),
    (
    // Azure Firewall Networking with ICMP
    materializedData
    | where msg_s has "Type="
    | parse msg_s with Protocol " Type=" ICMPType " request from " SourceIP " to " DestinationIP ". Action: " Action ". "
    ),
    (
    // Azure Firewall Networking DNAT rule - Standard
    materializedData
    | where msg_s has "DNAT'ed" and msg_s !has ". Rule Collection:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort " was " Action " to " NatDestination
    ),
    (
    // Azure Firewall Networking DNAT rule (firewall not using policy)
    materializedData
    | where msg_s has "DNAT'ed" and msg_s has ". Rule Collection:" and msg_s !has "Policy:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort " was " Action " to " NatDestination ". Rule Collection: " RuleCollection ". Rule: " Rule
    ),
    (
    // Azure Firewall Networking DNAT rule (firewall using policy)
    materializedData
    | where msg_s has "DNAT'ed" and msg_s has ". Rule Collection:" and msg_s has "Policy:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort " was " Action " to " NatDestination ". Policy: " Policy ". Rule Collection Group:" RuleCollectionGroup ". Rule Collection: " RuleCollection ". Rule: " Rule
    ),
    (
    // Azure Firewall Network rule (firewall not using policy)
    materializedData
    | where msg_s has "Rule Collection:" and msg_s !has "Policy:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort ". Action: " Action ". Rule Collection: " RuleCollection ". Rule Name: " Rule
    ),
    (
    // Azure Firewall Network rule (firewall using policy)
    materializedData
    | where msg_s has "Policy:"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort ". Action: " Action ". Policy:" Policy ". Rule Collection Group:" RuleCollectionGroup ". Rule Collection: " RuleCollection ". Rule Name: " Rule
    ),
    (
    // Azure Firewall Network IDS Policy
    materializedData
    | where msg_s has "IDS:"    
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort ". Action: " Action ". Signature:" Signature ". IDS: " Policy " Priority: " Priority ". Classification: " Classification
    ),
    (
    // Azure Firewall Network Threat Intel
    materializedData
    | where msg_s has "ThreatIntel"
    | parse msg_s with Protocol " request from " SourceIP ":" SourcePort " to " DestinationIP ":" DestinationPort ". Action: " Action ". ThreatIntel:" Policy
    )
| project-away msg_s